apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdkVersion propCompileSdkVersion
    buildToolsVersion propBuildToolsVersion

    defaultConfig {
        applicationId "de.limbusdev.tanookifilemanager"
        minSdkVersion propMinSdkVersion
        targetSdkVersion propTargetSdkVersion
        versionCode 1101
        versionName "6.8.6"
        multiDexEnabled true
        setProperty("archivesBaseName", "file-manager")
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        if (keystorePropertiesFile.exists()) {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

dependencies {
    implementation project(":commons")
    implementation 'com.github.Stericson:RootTools:df729dcb13'
    implementation 'com.github.Stericson:RootShell:1.6'
    implementation 'com.alexvasilkov:gesture-views:2.5.2'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    api 'com.github.bumptech.glide:glide:4.10.0'
    kapt 'com.github.bumptech.glide:compiler:4.10.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.10.0'
}
